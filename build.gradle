plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id("cn.lalaki.central") version "1.2.5"
}

group = 'dev.lasm'
version = '1.1.0'
archivesBaseName = 'yeahtext'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11

    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    withJavadocJar()
    withSourcesJar()
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
    options.encoding = 'UTF-8'
}

dependencies {
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
    compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

var localMavenRepo = uri("local_repo")

centralPortalPlus {
    url = localMavenRepo
    username = sonatypeUsername
    password = sonatypePassword

}

publishing {
    repositories {
        maven {
            url = localMavenRepo
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'YeahText'
                description = 'Unicode text styling library'
                url = 'https://github.com/LasmGratel/YeahText'
                packaging = 'jar'
                properties = [
                        myProp: "value",
                        "prop.with.dots": "anotherValue"
                ]
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'LasmGratel'
                        name = 'Lasm Gratel'
                        email = 'lasm_gratel@hotmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/LasmGratel/YeahText.git'
                    developerConnection = 'scm:git:ssh://github.com/LasmGratel/YeahText.git'
                    url = 'https://github.com/LasmGratel/YeahText'
                }
            }
        }
    }
}
signing {
    sign publishing.publications.mavenJava
}